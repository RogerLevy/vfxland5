\ =============================================================================
\ Color and position
\ =============================================================================

\ Color

ec-cell penr \ fixed
ec-cell peng \ fixed
ec-cell penb \ fixed
ec-cell pena \ fixed
ec-cell ppenx \ fixed
ec-cell ppeny \ fixed

: frgba@ ( - f:r f:g f:b f:a )
    penr@ p>f peng@ p>f penb@ p>f pena@ p>f ; 

: frgba ( f:r f:g f:b f:a - )
    f>p pena! f>p penb! f>p peng! f>p penr! ;

1e 1e 1e 1e frgba

: rgba ( n - )  
    c>f 8 << c>f 8 << c>f 8 << c>f drop frgba ;

\ Position
: at ( x y - ) >p ppeny! >p ppenx! ;
: penx@ ( - x ) ppenx@ >i ;
: peny@ ( - y ) ppeny@ >i ;
: at@ ( - x y ) penx@ peny@ ;
: at@f ( - f:x f:y ) ppenx@ p>f ppeny@ p>f ;
: +at ( x y - ) at@ 2+ at ;
: <at  you { ppenx@ ppeny@ } ppeny! ppenx! ;  \ hard-inherit pen position

\ =============================================================================
\ Drawing primitives
\ =============================================================================

: albmpw ( albmp - w ) dup if al_get_bitmap_width then ;
: albmph ( albmp - h ) dup if al_get_bitmap_height then ;

: draw-bitmap ( albmp n - )
    over if at@f al_draw_bitmap else 2drop then ;

: *subbmp ( albmp w h - albmp' )
    at@ 2swap al_create_sub_bitmap ?dup 
    0= abort" al_create_sub_bitmap error" ;

: -albmp ( albmp - )
    ?dup -exit al_destroy_bitmap ;

redef : cls ( - )
    0e 0e 0e 1e al_clear_to_color ;

\ =============================================================================
\ Matrix stack
\ =============================================================================

|| create mstk 16 cells 16 * allot
|| create m2 16 cells allot
|| variable mp
|| variable tx variable ty
|| : m mp @ 15 and 16 cells * mstk + ; 

: m{ ( - )
    m 1 mp +! m swap al_copy_transform ;

: m} ( - )
    -1 mp +!  m al_use_transform ;

: transform ( f:x f:y f:sx f:sy f:a - )
    m2 al_build_transform  m2 m al_compose_transform  m m2 al_copy_transform
    m al_use_transform ;

: identity ( - )
    m al_identity_transform  m al_use_transform ;

: 2p*m ( x. y. - x. y. )
    2p>f ty sf! tx sf!  m tx ty al_transform_coordinates
    tx sf@ f>p ty sf@ f>p ;

: inverse ( - )
    m al_invert_transform ;

\ =============================================================================
\ Text
\ =============================================================================

\ Variables

\ || variable rmargin
variable line-spacing  8 line-spacing !
ec-cell fnt

\ Text rendering

: mltext ( a len w - )
    {: | zstr[ 4096 ] lh w z :}
    over 0= if 3drop exit then 
    >r zstr[ zplace  zstr[ r> s>f f>ds   
    line-spacing @ s>f f>ds to lh to w to z 
    fnt@  frgba@  at@f w lh 0 z al_draw_multiline_text ;

: print ( a len - )
    winw penx@ - mltext ;

: print2 ( a len line-spacing - )
    line-spacing @ >r line-spacing !
    print
    r> line-spacing ! ;

: textw ( a n - n )
    {: | zstr[ 4096 ] :}
    zstr[ zplace  zstr[  fnt@  swap  al_get_text_width ;  

: glyph ( n - )
    >r  fnt@ frgba@ at@f  r> al_draw_glyph ;
