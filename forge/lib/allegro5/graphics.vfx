\ =============================================================================
\ Color and position
\ =============================================================================

\ Color

ec-cell ppenr \ fixed
ec-cell ppeng \ fixed
ec-cell ppenb \ fixed
ec-cell ppena \ fixed
ec-cell ppenx \ fixed
ec-cell ppeny \ fixed

: rgba@fds
    ppenr@ p>f f>ds
    ppeng@ p>f f>ds
    ppenb@ p>f f>ds
    ppena@ p>f f>ds    
;

: rgba ( r. g. b. a. - )
    ppena! ppenb! ppeng! ppenr! ;

1. 1. 1. 1. rgba

: c>p  $ff and s>f 255e f/ f>p ;

: rgba8 ( n - )  
    dup c>p >r 8 >>
    dup c>p >r 8 >> 
    dup c>p >r 8 >> 
    c>p r> r> r> rgba ;

\ Position
: at ( x y - ) >p ppeny! >p ppenx! ;
: atp ( x. y. - ) ppeny! ppenx! ;
: penx@ ( - x ) ppenx@ >i ;
: peny@ ( - y ) ppeny@ >i ;
: at@ ( - x y ) penx@ peny@ ;
: at@p ( - x. y. ) ppenx@ ppeny@ ;
: at@f ( - f:x f:y ) ppenx@ p>f ppeny@ p>f ;
: +at ( x y - ) 2>p at@p 2+ atp ;
: <at  you { ppenx@ ppeny@ } ppeny! ppenx! ;  \ hard-inherit pen position

\ =============================================================================
\ Drawing primitives
\ =============================================================================

: albmpw ( albmp - w ) dup if al_get_bitmap_width then ;
: albmph ( albmp - h ) dup if al_get_bitmap_height then ;

: draw-bitmap ( albmp n - )
    over if at@f al_draw_bitmap else 2drop then ;

: *subbmp ( albmp w h - albmp' )
    at@ 2swap al_create_sub_bitmap ?dup 
    0= abort" al_create_sub_bitmap error" ;

: -albmp ( albmp - )
    ?dup -exit al_destroy_bitmap ;

redef : cls ( - )
    0e 0e 0e 1e al_clear_to_color ;

\ =============================================================================
\ Matrix stack
\ =============================================================================

|| create mstk 16 cells 16 * allot
|| create m2 16 cells allot
|| variable mp
|| variable tx variable ty
|| : m mp @ 15 and 16 cells * mstk + ; 

: m{ ( - )
    m 1 mp +! m swap al_copy_transform ;

: m} ( - )
    -1 mp +!  m al_use_transform ;

: transform ( f:x f:y f:sx f:sy f:a - )
    m2 al_build_transform  m2 m al_compose_transform  m m2 al_copy_transform
    m al_use_transform ;

: identity ( - )
    m al_identity_transform  m al_use_transform ;

: 2p*m ( x. y. - x. y. )
    2p>f ty sf! tx sf!  m tx ty al_transform_coordinates
    tx sf@ f>p ty sf@ f>p ;

: inverse ( - )
    m al_invert_transform ;

\ =============================================================================
\ Text
\ =============================================================================

\ Variables

\ || variable rmargin
variable line-spacing  8 line-spacing !
ec-cell fnt

\ Text rendering

: mltext ( a len w - )
    {: a len w | lh zstr :}
    a len or -exit
    a len *zbuf to zstr
    w s>f f>ds to w
    line-spacing @ s>f f>ds to lh 
    fnt@  rgba@fds  at@f w lh 0 zstr al_draw_multiline_text 
    zstr free throw ;

: print ( a len - )
    winw penx@ - mltext ;

: print2 ( a len line-spacing - )
    line-spacing @ >r line-spacing !
    print
    r> line-spacing ! ;

: textw ( a n - n )
    *zbuf >r  fnt@  r@  al_get_text_width  r> free throw ;  

: textbox ( a n - x y w h )
    {: | buf[ 16 ] :}
    *zbuf >r
    fnt@  r@  buf[ dup cell+ dup cell+ dup cell+ al_get_text_dimensions
    r> free throw
    buf[ @+ swap @+ swap @+ swap @ ;

: glyph ( n - )
    >r  fnt@ rgba@fds at@f  r> al_draw_glyph ;
