\ ==============================================================================
\ Serialization Support (JSON)
\ Supports serialization of NIBS objects using cjson2
\ ==============================================================================

require %idir%/cjson2.vfx

private

    also cjson2>>

    0 value val-addr

    : ser-json-cell    ( zname cjson - ) val-addr @ -rot add-int ;
    : ser-json-int     ( zname cjson - ) val-addr @ -rot add-int ;
    : ser-json-fixed   ( zname cjson - ) val-addr @ -rot add-fixed ;
    : ser-json-float   ( zname cjson - ) val-addr sf@ add-float ;
    : ser-json-cstring ( zname cjson - ) val-addr count >zpad -rot add-string ;
    : ser-json-lstring ( zname cjson - )
        val-addr @ ?dup if lcount else s" " then >zpad -rot add-string ;

    create ser-json-table
        ' ser-json-cell ,
        ' ser-json-int ,
        ' ser-json-fixed ,
        ' ser-json-float ,
        ' ser-json-cstring ,
        ' ser-json-lstring ,

    : ser-property-json  ( cjson property - )
        dup >zname -rot
        cla @ >property-type @ cells ser-json-table + @ execute ;

    : des-json-cell    ( zname cjson - ) swap j>n val-addr ! ;
    : des-json-int     ( zname cjson - ) swap j>n val-addr ! ;
    : des-json-fixed   ( zname cjson - ) swap j>p val-addr ! ;
    : des-json-float   ( zname cjson - ) swap j>f val-addr sf! ;
    : des-json-cstring ( zname cjson - ) swap j>s val-addr place ;
    : des-json-lstring ( zname cjson - )
        swap j>s dup if
            dup cell+ allocate throw dup val-addr !
            lplace
        else 2drop 0 val-addr ! then ;

    create des-json-table
        ' des-json-cell ,
        ' des-json-int ,
        ' des-json-fixed ,
        ' des-json-float ,
        ' des-json-cstring ,
        ' des-json-lstring ,

    : des-property-json  ( cjson property - )
        dup >zname -rot
        cla @ >property-type @ cells des-json-table + @ execute ;

public
also cjson2>>
borrow nib2>> prop+

: add-properties-to ( obj cjson - )
    {: obj cjson | property0 tmpl-addr :}
    obj {
        cla @ >zname z" class" cjson add-string

        cla @ -> property-list list-count for
            list-next to property0
            property0 cla @ >serialize-flag @  if

                \ get the property address in the instance and in the template
                me property0 prop+ to val-addr
                cla @ >template property0 prop+ to tmpl-addr

                cjson property0 ser-property-json
            then
        loop drop
    } ;

: save-as-json ( object path len - )
    serjson> add-properties-to ;

: *json ( object - cjson )
    new-object dup >r add-properties-to r> ;

: read-props ( cjson obj - )
    {: cjson obj | property0 :}
    obj {
        cla @ -> property-list list-count for
            list-next to property0
            property0 cla @ >serialize-flag @  if
                property0 >zname cjson swap ?get-item if
                    me property0 prop+ to val-addr
                    cjson property0 des-property-json
                then
            then
        loop drop
    } ;

: load-json ( path len - cjson )
    load-file udup parse-json swap free drop ;

