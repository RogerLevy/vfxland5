require %idir%/checks.vfx
require %idir%/utilities.vfx
require %idir%/traits.vfx
require %idir%/asset.vfx

65536 ?constant #bitmaps

class: %bitmap
    %asset derive
    prop id 
    prop albmp
class;

|| %bitmap object dummy.bmp
|| : dummies,  #bitmaps for dummy.bmp , loop ;

create bitmaps %addr array{ dummies, array} drop

: bitmap@  $FFFF and bitmaps el@ ;
: bitmap!  $FFFF and bitmaps el! ;

variable next-bmp#  1 next-bmp# !

: alloc-bmp# ( - bmpid ) next-bmp# @   1 next-bmp# +! ;
: add-bitmap ( bitmap - bmpid ) alloc-bmp# dup >r bitmap! r> ;

: id>albmp ( bmpid - albmp ) 
    bitmap@ -> albmp @ ;

: id>bmp ( bmpid - bitmap ) 
    bitmap@ ;

: albmp>id ( albmp - bmpid | 0 )
    \ Find bitmap ID given an Allegro bitmap pointer
    \ Returns 0 if not found
    next-bmp# @ 1 do
        dup i bitmap@ -> albmp @ = if
            drop i unloop exit
        then
    loop
    drop 0 ;

: bmp>parent ( bmpid - bmpid' )  
    id>albmp al_get_parent_bitmap albmp>id ;

: is-subbmp? ( bmpid - flag )
    id>albmp al_is_sub_bitmap 0<> ;

: ?bmp>parent ( bmpid - bmpid|bmpid' )
    dup is-subbmp? if bmp>parent then ;

: bitmap-file ( bmpid - a n ) 
    ?bmp>parent id>bmp -> srcpath$ count ;

: bmp>ts ( bmpid - name len )
    bitmap-file -path -ext f" %s.ts" evaluate ;

\ : ?nullbmp  dup 0= abort" BITMAP-FILE : Bitmap is null!" ;
\ : ?subbmp  dup al_is_sub_bitmlap if al_get_parent_bitmap then ;

: wrap-bitmap ( albmp - bitmap )
    %bitmap make { albmp ! me } ;

: bmpwh ( bmpid - w h )
    id>albmp ?dup if dup albmpw swap albmph else 0 0 then ;

: bmpw ( bmpid - w )
    bmpwh drop ;

: bmph ( bmpid - h )
    bmpwh nip ;

: put ( bmpid - ) 
    \ draw a sprite; takes a bitfield $F000IIII 
    ?dup -exit
    dup id>albmp swap 24 rshift draw-bitmap ;

: cput ( bmpid - ) 
    \ draw sprite with center as the origin
    dup dup bmpwh -2 -2 2/ +at put bmpwh 2 2 2/ +at ;

\ =============================================================================
\ Backward compatibility
\ =============================================================================

\ aka >albmp id>albmp
aka bmptotem id>bmp
