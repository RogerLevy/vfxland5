require %idir%/checks.vfx
require %idir%/utilities.vfx
require %idir%/traits.vfx
require %idir%/asset.vfx

65536 ?constant #bitmaps

class: %bitmap
    %asset derive
    is-a %saveable
    prop id 
    prop albmp
    prop touched
class;

%bitmap object dummy.bmp
|| : dummies,  #bitmaps for dummy.bmp , loop ;

create bitmaps %addr array{ dummies, array} drop

: bitmap@  $FFFF and bitmaps el@ ;
: bitmap!  $FFFF and bitmaps el! ;

variable next-bmp#  1 next-bmp# !

: alloc-bmp# ( - bmpid ) next-bmp# @   1 next-bmp# +! ;
: add-bitmap ( bitmap - bmpid ) 
    alloc-bmp# >r 
    r@ over >id !
    r@ bitmap! r> ;

: id>albmp ( bmpid - albmp ) 
    bitmap@ -> albmp @ ;

: id>bmp ( bmpid - bitmap ) 
    bitmap@ ;

: albmp>id ( albmp - bmpid | 0 )
    \ Find bitmap ID given an Allegro bitmap pointer
    \ Returns 0 if not found
    next-bmp# @ 1 do
        dup i bitmap@ -> albmp @ = if
            drop i unloop exit
        then
    loop
    drop 0 ;

: bmp>parent ( bmpid - bmpid' )  
    id>albmp al_get_parent_bitmap albmp>id ;

: is-subbmp? ( bmpid - flag )
    id>albmp dup -exit al_is_sub_bitmap 0<> ;

: ?bmp>parent ( bmpid - bmpid|bmpid' )
    dup is-subbmp? if bmp>parent then ;

: bitmap-file ( bmpid - a n ) 
    ?bmp>parent id>bmp -> srcpath$ count ;

: bmp>ts$ ( bmpid - name len )
    bitmap-file -path -ext f" %s.ts" evaluate ;

\ : ?nullbmp  dup 0= abort" BITMAP-FILE : Bitmap is null!" ;
\ : ?subbmp  dup al_is_sub_bitmlap if al_get_parent_bitmap then ;

: wrap-bitmap ( albmp - bitmap )
    %bitmap make { albmp ! me } ;

: bmpwh ( bmpid - w h )
    id>albmp ?dup if dup albmpw swap albmph else 0 0 then ;

: bmpw ( bmpid - w )
    bmpwh drop ;

: bmph ( bmpid - h )
    bmpwh nip ;

: put ( bmpid - ) 
    \ draw a sprite; takes a bitfield $F000IIII 
    ?dup -exit
    dup id>albmp swap 24 rshift draw-bitmap ;

: cput ( bmpid - ) 
    \ draw sprite with center as the origin
    dup dup bmpwh -2 -2 2/ +at put bmpwh 2 2 2/ +at ;

%bitmap :: save
    touched @ -exit
    id @ id>albmp 0= ?exit  \ Skip if no valid Allegro bitmap
    id @ is-subbmp? if
        \ For sub-bitmaps, touch parent and exit
        touched off
        id @ bmp>parent ?dup if
            id>bmp >touched on
        then
        exit
    then
    \ Normal bitmap - save it
    touched off
    id @ bitmap-file expand >zpad   id @ id>albmp al_save_bitmap
        0= abort" Error saving bitmap"
    id @ bitmap-file f" Saved bitmap to %s" type cr ;

: save-bitmaps
    \ Do 2 passes - allows touched sub-bitmaps to touch
    \ their parent bitmaps which are then saved once in the second pass
    2 for ['] save bitmaps each loop ;


\ =============================================================================
\ Backward compatibility
\ =============================================================================

\ aka >albmp id>albmp
aka bmptotem id>bmp
