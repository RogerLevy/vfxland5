require %idir%/checks.vfx
require %idir%/utilities.vfx
require %idir%/actor.vfx
require %idir%/stage.vfx
require %idir%/bitmap.vfx
require %idir%/tileset.vfx

\ =============================================================================
\ Actor sprite support
\ =============================================================================

class: %actor
    is-a %_bitmap
class;

%actor :: draw ( - )
    bmp @ -exit  x 2@ 2>i at bmp @ cput ;

: ?pillory ( - )
    me benched? if
        $ff5555ff rgba8 s" !" print
    then ;

: backsprites ( - )
    ec> batch> actives> prio @ ?exit me draw ?pillory ;

: sprites ( - )
    ec> batch> actives> prio @ -exit me draw ?pillory ;

\ =============================================================================
\ Frame animation
\ =============================================================================

trait: %animator
    prop anm :addr <readonly
    prop a.ts :ref %tileset <readonly
    prop a.spd :fixed
    prop a.len
    prop a.ofs :fixed
    prop a.done 
    prop a.flp
trait;

class: %actor
    is-a %animator
class;

|| variable alen \ pointer to len in animation header
|| : >frames ( anim - a ) cell+ cell+ ;
|| : ?baseid  a.ts @ dup if 0 tile then ;
|| : the-frame  a.ofs @ >i cells anm @ >frames + @ ?baseid + a.flp @ xor ;
|| : ?done  a.ofs @ a.spd @ + a.len @ >p >= -exit  a.done on ;

: cycle ( anim spd. - ) \ to stop, say `anm off`
    a.spd !  dup anm !  @+ a.ts ! @ a.len !  0 a.ofs !  a.done off  
    the-frame bmp ! ;

: animation ( ts <name> - )  \ tileset , len , ofs , ofs , ofs , ... ofs ,
    create , here alen ! 0 , ;

: frame, ( n - )
    , 1 alen @ +! ;

: range, ( n n - )
    2dup < if 1 + swap do i frame, loop
    else swap do i frame, -1 +loop then ;

: advance-animation ( spd. - bmpid )    
    a.spd @ p* a.ofs @ + a.len @ >p mod a.ofs !
    ?done  the-frame ;

: animate ( - )
    hold @ ?exit actives> anm @ -exit 1.0 advance-animation bmp ! ;


