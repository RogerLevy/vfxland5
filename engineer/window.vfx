\ =============================================================================
\ Window position save/restore
\ =============================================================================

variable winx
variable winy

: session-ini-path ( - a n )
    s" %home%/session.ini" slashes expand ;

: save-window-pos
    fullscreen? ?exit
    display winx winy al_get_window_position
    s{
        .sintvar winx 
        .sintvar winy 
    s} session-ini-path write ;

: restore-window-pos
    fullscreen? ?exit
    session-ini-path CfgIncluded
    display winx @ winy @ 25 max al_set_window_position ;

['] save-window-pos AtExit

\ =============================================================================
\ Window management
\ =============================================================================

extern void * GetForegroundWindow( );
extern bool SetForegroundWindow( void * hwnd );
GetForegroundWindow value vfx-hwnd

: >display ( - )
    flushOP-gen drop \ flush buffered output (VFX)
    reset-keyboard 
    display al_get_win_window_handle SetForegroundWindow drop ;

: >vfx  vfx-hwnd SetForegroundWindow drop ;
: fullscreen  ." FULLSCREEN" cr  -1 to fullscreen? ;
: windowed  ." WINDOWED" cr  0 to fullscreen? ;
: *display  create-display  restore-window-pos ;

: winmode ( - )
    2 zoom ! 
    appw apph fullscreen? resize-display
    fullscreen? not if
        display winx @ winy @ 25 max al_set_window_position 
    then ;