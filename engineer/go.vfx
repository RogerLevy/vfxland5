\ =============================================================================
\ Framerate independence
\ =============================================================================

redef : ticks  ( n - ms )
    s>f 1000e tps f/ f* f>s ;

: update-delta  ( - )
    ustime  ucounter 2dup to ustime  2swap 2-
        d>s to usdelta
    usdelta s>f 1000000e f/ to sdelta
    ustime 1000 m/ to mstime
    sdelta f>p to pdelta ;

|| : spin ( - )
    {: | ev[ /ALLEGRO_EVENT ] :}
    frame-queue ev[ al_wait_for_event
    frame-queue al_flush_event_queue ;

\ =============================================================================
\ Main Loop Support
\ =============================================================================

private

: poll ( - )
    poll-keyboard  
    <alt> held? <altgr> held? or to alt? 
    <lshift> held? <rshift> held? or to shift? 
    <lctrl> held? <rctrl> held? or to ctrl? ;

: w/f ( - )
    \ Display mode switching for release builds
    fullscreen? if windowed else save-window-pos fullscreen then
    winmode  
    update-delta update-delta ;

: ?idle  debug @ if (EmptyIdle) then ;

public

: present  ?idle display al_flip_display spin ;

\ =============================================================================
\ Event handler
\ =============================================================================

private
: system-events ( - )
    ALLEGRO_EVENT_DISPLAY_CLOSE happened? if
        bye
    then
    ALLEGRO_EVENT_DISPLAY_SWITCH_OUT happened? if
        reset-keyboard reset-mouse
    then
    ALLEGRO_EVENT_DISPLAY_RESIZE happened? if
        display al_acknowledge_resize
    then ;

|| : ?event ( - flag )
    alqueue the-event al_get_next_event 0<> dup -exit
    >r system-events r> ;

: pump ( - )
    [: need-pump @ -exit  begin ?event not until ;] catch  
    need-pump on  .throw ;

redef : event? ( - flag )
    repl @ if 0 exit then
    need-pump off ?event ;

\ =============================================================================
\ Main Loop
\ =============================================================================

public

: ?quit  esc-quits @ -exit  <esc> pressed? shift? not and throw ;
: ?w/f  <enter> pressed?  alt? and if w/f -keys then ;
: ?bye  <f4> pressed?  alt? and if bye then ;
: system-controls  ?quit ?w/f ?bye ;

public

: process-input  poll pump system-controls ;
: draw-game  cls 0 0 at 'show @ execute ;
: process-tick  {{ ['] draw-game draw-tv }} ;
: frame  update-delta process-input process-tick present ;

|| : pre ( - )
    >display 
    0 #tib ! postpone \\ 
    update-delta update-delta 
    need-pump on 
    going on 
    frame-timer al_resume_timer 
    spin ;

|| : post ( - )
    frame-timer al_stop_timer 
    going off
    fullscreen? if windowed winmode then
    >vfx ;

: go ( - ) 
    going @ ?exit pre 
    begin ['] frame catch ?dup until 
    post throw ;

continuation: show> ( - ) ( - ) 'show ! ; 
